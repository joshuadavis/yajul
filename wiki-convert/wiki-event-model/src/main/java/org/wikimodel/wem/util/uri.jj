
    // URI syntax - see http://tools.ietf.org/html/rfc3986#page-49
      <#URI: <URI_SCHEME> ":" <URI_HIER_PART> ("?" <URI_QUERY>)? ("#" <URI_FRAGMENT>)? >
    
    | <#ALPHA: ["A"-"z"]>
    | <#DIGIT: ["0"-"9"]>
    | <#HEXDIG: ( <DIGIT> | ["A"-"F"] | ["a"-"f"] ) >
    | <#DEC_OCTET: (
        <DIGIT>                  // 0-9
        | ["1"-"9"] <DIGIT>      // 10-99
        | "1" <DIGIT> <DIGIT>    // 100-199
        | "2" ["0"-"4"] <DIGIT>  // 200-249
        | "25" ["0"-"5"]         // 250-255
      )>  
    | <#URI_GEN_DELIMS: [ ":", "/", "?", "#", "[", "]", "@" ]>
    | <#URI_SUB_DELIMS: [ "!", "$", "&", "'", "(", ")", "*", "+", ",", ";", "=" ]>
    | <#URI_UNRESERVED: ( <ALPHA> | <DIGIT> | "-" | "." | "_" | "~" )>
    | <#URI_RESERVED: ( <URI_GEN_DELIMS> | <URI_SUB_DELIMS> ) >
    | <#URI_SCHEME: <ALPHA> ( <ALPHA> | <DIGIT> | "+" | "-" | "." )* >
    | <#URI_PCT_ENCODED: "%" <HEXDIG> <HEXDIG> >
    | <#URI_PCHAR:  ( <URI_UNRESERVED> | <URI_PCT_ENCODED> | <URI_SUB_DELIMS> | ":" | "@" ) >
    | <#URI_QUERY:    ( <URI_PCHAR> | "/" | "?" )* >
    | <#URI_FRAGMENT: ( <URI_PCHAR> | "/" | "?" )* >

    | <#URI_AUTHORITY: ( <URI_USERINFO> "@" )? <URI_HOST> ( ":" <URI_PORT> )? >
    | <#URI_USERINFO: ( <URI_UNRESERVED> | <URI_PCT_ENCODED> | <URI_SUB_DELIMS> | ":" )* >
    | <#URI_HOST: ( <URI_IP_LITERAL> | <URI_IP_V4_ADDRESS> | <URI_REG_NAME> ) >
    | <#URI_REG_NAME: ( <URI_UNRESERVED> | <URI_PCT_ENCODED> | <URI_SUB_DELIMS> )* >
    | <#URI_PORT: (<DIGIT>)* >

    | <#URI_IP_LITERAL: "[" ( <URI_IP_V6_ADDRESS> | <URI_IP_V_FUTURE>  ) "]" >
    | <#URI_IP_V_FUTURE : "v" ( <HEXDIG> )+ "." ( <URI_UNRESERVED> | <URI_SUB_DELIMS> | ":" )+ > 
    | <#URI_IP_V4_ADDRESS: <DEC_OCTET> "." <DEC_OCTET> "." <DEC_OCTET> "." <DEC_OCTET> >

    | <#URI_IP_V6_ADDRESS: ( 
                                                      ( <URI_H16> ":" ){6} <URI_LS32>
        |                                       "::"  ( <URI_H16> ":" ){5} <URI_LS32>
        | (                        <URI_H16> )? "::"  ( <URI_H16> ":" ){4} <URI_LS32>
        | ( ( <URI_H16> ":" ){0,1} <URI_H16> )? "::"  ( <URI_H16> ":" ){3} <URI_LS32>
        | ( ( <URI_H16> ":" ){0,2} <URI_H16> )? "::"  ( <URI_H16> ":" ){2} <URI_LS32>
        | ( ( <URI_H16> ":" ){0,3} <URI_H16> )? "::"    <URI_H16> ":"      <URI_LS32>
        | ( ( <URI_H16> ":" ){0,4} <URI_H16> )? "::"                       <URI_LS32>
        | ( ( <URI_H16> ":" ){0,5} <URI_H16> )? "::"                       <URI_H16>
        | ( ( <URI_H16> ":" ){0,6} <URI_H16> )? "::"
    ) >
    | <#URI_H16:  (<HEXDIG>){1,4} >
    | <#URI_LS32: ( <URI_H16> ":" <URI_H16> ) | <URI_IP_V4_ADDRESS> >
    
    | <#URI_PATH_ABEMPTY: ( "/" <URI_SEGMENT> )* >
    | <#URI_PATH_ABSOLUTE: "/" ( <URI_SEGMENT_NZ> ( "/" <URI_SEGMENT> )* )? >
    | <#URI_PATH_ROOTLESS: <URI_SEGMENT_NZ> ( "/" <URI_SEGMENT> )* >
    | <#URI_SEGMENT: (<URI_PCHAR>)* >
    | <#URI_SEGMENT_NZ: (<URI_PCHAR>)+ >
    
    // A simplified URI definition: it does not contain an empty path.
    | <#URI_HIER_PART: ( "//" <URI_AUTHORITY> <URI_PATH_ABEMPTY> | <URI_PATH_ABSOLUTE> | <URI_PATH_ROOTLESS> )>
